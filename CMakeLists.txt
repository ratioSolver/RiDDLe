cmake_minimum_required(VERSION 3.21)
project(RiDDLe VERSION 0.2.0 LANGUAGES CXX)

include(GNUInstallDirs)
include(CMakePackageConfigHelpers) 

include(CTest)
enable_testing()

set(HEURISTIC_TYPES h_max h_add)
set(HEURISTIC_TYPE h_max CACHE STRING "Heuristic type")
set_property(CACHE HEURISTIC_TYPE PROPERTY STRINGS ${HEURISTIC_TYPES})
list(FIND HEURISTIC_TYPES ${HEURISTIC_TYPE} HEURISTIC_TYPE_INDEX)
if(${HEURISTIC_TYPE_INDEX} EQUAL -1)
    message(FATAL_ERROR "Invalid heuristic type ${HEURISTIC_TYPE}")
endif()

option(COMPUTE_NAMES "Compute RiDDLe names" OFF)

add_library(RiDDLe src/core.cpp src/scope.cpp src/env.cpp src/type.cpp src/timeline.cpp src/constructor.cpp src/method.cpp src/term.cpp src/conjunction.cpp src/declaration.cpp src/statement.cpp src/expression.cpp src/compilation_unit.cpp src/lexer.cpp src/parser.cpp src/items.cpp src/types.cpp src/flaw.cpp src/resolver.cpp)
add_library(ratio::RiDDLe ALIAS RiDDLe)
target_compile_features(RiDDLe PUBLIC cxx_std_17)
target_include_directories(RiDDLe PUBLIC $<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/include> $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>)
if(NOT TARGET json)
    add_subdirectory(extern/json)
endif()
add_dependencies(RiDDLe json)
target_link_libraries(RiDDLe PUBLIC json)
setup_sanitizers(RiDDLe)

message(STATUS "Heuristic type: ${HEURISTIC_TYPE}")
if(${HEURISTIC_TYPE} STREQUAL "h_max")
    target_compile_definitions(RiDDLe PUBLIC H_MAX)
elseif(${HEURISTIC_TYPE} STREQUAL "h_add")
    target_compile_definitions(RiDDLe PUBLIC H_ADD)
endif()

message(STATUS "Compute RiDDLe names: ${COMPUTE_NAMES}")
if(COMPUTE_NAMES)
    target_compile_definitions(RiDDLe PUBLIC COMPUTE_NAMES)
endif()

if(BUILD_TESTING)
    add_subdirectory(tests)
endif()

install(TARGETS RiDDLe EXPORT RiDDLe-targets LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR} ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR} RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR})
install(DIRECTORY "${PROJECT_SOURCE_DIR}/include/" DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})
install(EXPORT RiDDLe-targets FILE RiDDLe-targets.cmake NAMESPACE RiDDLe:: DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/RiDDLe)
configure_package_config_file(cmake/RiDDLe-config.cmake.in RiDDLe-config.cmake INSTALL_DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/RiDDLe)
write_basic_package_version_file(RiDDLe-version.cmake COMPATIBILITY SameMajorVersion)
install(FILES "${CMAKE_CURRENT_BINARY_DIR}/RiDDLe-config.cmake" "${CMAKE_CURRENT_BINARY_DIR}/RiDDLe-version.cmake" DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/RiDDLe)

set(CPACK_PROJECT_NAME RiDDLe)
set(CPACK_PROJECT_VERSION ${PROJECT_VERSION})
include(CPack)
